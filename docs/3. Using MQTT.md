# Using MQTT

## MQTT Library

[This](https://github.com/arduino-libraries/ArduinoMqttClient) library is being used.

## What is MQTT

MQTT is a lightweight push-subscribe, machine-to-machine network protocol for message queue service. This service helps us to send and receive data to and from microcontrollers, such as Arduino and EV3.

This section explores the possibility of using MQTT and implementing a [Sling](https://github.com/source-academy/sling)-like functionality on Arduino. The aim is to send and receive data to Arduino. This includes programs to be run and its return values.

## Topics

- Arduino subscribe:
    - `/run` : Runs a program
    - `/stop` : Stops the currently running program (Will be covered in the next section)
    - `/ping` : Returns the current status after a ping topic is received
- Client subscribe:
    - `/hello` : Arduino pings the client when it starts up using this topic
    - `/status` : Current status of the Arduino
    - `/display` : Displays the return status of Arduino (after a program terminates)

## Create a local MQTT server

This example uses [mosquitto](https://mosquitto.org) as server. Below is the configuration file.

```
# Default listener
listener 1883
protocol mqtt
allow_anonymous true

# MQTT over WebSockets
listener 9001
protocol websockets
allow_anonymous true
```

The server can be started with the following command: `mosquitto -c mosquitto.conf`

## Program Hexdump

This command is used for hexdump of a SVML program.

```bash
xxd -p program.svm >> program
```

## Sending program

This command sends a program to Arduino using `mosquitto_pub` to the `arduino/run` topic.

```bash
mosquitto_pub -h <Host IP> -m "<program hexdump without newline>" -t arduino/run
```

## Subscribe and receive messages from topics

`mosquitto_sub` is used to subscribes to the relevant topics

```bash
mosquitto_sub -h <Host IP> -t arduino/display -t arduino/hello -t arduino/status
```

## The Arduino sketch

A state machine is implemented on the Arduino with 3 states: `IDLE`, `RUN` and `HELLO`. The device will return to the `IDLE` state after any given state by default.

Looking at `onMqttReceive`, the state is changed when the board receives `stop_topic` and `run_topic`.

When the board receives `ping_topic`, the state will not be changed and the `echo()` function will be called.

Flow:

1. The device is initialized with a `HELLO` state that sends a `HELLO` message to the server. After that, it changes its state to `IDLE`.
2.  The device stays `IDLE` and awaits `/ping` and `/run` messages.
    1. On `/ping`, the device sends a `/status` of 0 (`IDLE`).
    2. On `/run`, the device runs the given program.
3. The device `RUN` a program and awaits `/ping` and `/stop` messages.
    1. On `/ping`, the device sends a `/status` of 1 (`RUN`).
    2. On `/stop`, the device stops the given program.
4. When the device finishes running a program, whether it is terminated by the user or returns naturally, the device will send a `/display` message back to the server, right before returning to the `IDLE` state.

### Using Timer5

[Timer5](https://github.com/michael71/Timer5) is used to invoke callback functions. This is important for stopping the program when a program is running.

```cpp
// SlingWithSinter.ino
#include <ArduinoMqttClient.h>
#if defined(ARDUINO_SAMD_MKRWIFI1010) || defined(ARDUINO_SAMD_NANO_33_IOT) || defined(ARDUINO_AVR_UNO_WIFI_REV2)
  #include <WiFiNINA.h>
#elif defined(ARDUINO_SAMD_MKR1000)
  #include <WiFi101.h>
#elif defined(ARDUINO_ARCH_ESP8266)
  #include <ESP8266WiFi.h>
#elif defined(ARDUINO_ARCH_ESP32)
  #include <WiFi.h>
#endif

#include <sinter.h>
#include "Timer5.h"
#include "SevSegSource.h"
#include "internal_functions.h"
#include "arduino_secrets.h"
// This is used to store the IP, WiFi name and WiFi password

const char ssid[] = SECRET_SSID;
const char pass[] = SECRET_PASS;
WiFiClient wifiClient;
MqttClient mqttClient(wifiClient);

const char broker[]  = HOSTNAME;
int        port      = 1883;
char hello_topic[30] = SECRET_KEY;
char run_topic[30]   = SECRET_KEY;
char stop_topic[30]  = SECRET_KEY;
char ping_topic[30]  = SECRET_KEY;
char status_topic[30]  = SECRET_KEY;
char display_topic[30] = SECRET_KEY;
int messageSize = 0;

char heap[0x4000];
unsigned char program_svm[5000];
unsigned int program_svm_len = 0;

enum state_type_t {
  IDLE = 0,
  RUN,
  HELLO
};
state_type_t state = HELLO;
bool isRunning = false;

void setup() {
  Serial.begin(9600);

  Serial.print("Attempting to connect to WPA SSID: ");
  Serial.println(ssid);
  while (WiFi.begin(ssid, pass) != WL_CONNECTED) {
    Serial.print(".");
    delay(5000);
  }
  Serial.println("You're connected to the network");

  Serial.print("Attempting to connect to the MQTT broker: ");
  Serial.println(broker);
  if (!mqttClient.connect(broker, port)) {
    Serial.print("MQTT connection failed! Error code = ");
    Serial.println(mqttClient.connectError());
    while (1);
  }
  Serial.println("You're connected to the MQTT broker!");

  strcat(hello_topic, "/hello");
  strcat(run_topic, "/run");
  strcat(stop_topic, "/stop");
  strcat(ping_topic, "/ping");
  strcat(status_topic, "/status");
  strcat(display_topic, "/display");

  // Client to device topics
  mqttClient.subscribe(run_topic);
  mqttClient.subscribe(stop_topic);
  mqttClient.subscribe(ping_topic);
  mqttClient.onMessage(onMqttMessage);

  sinter_setup_heap(heap, 0x4000);
  setupInternals();

  MyTimer5.begin(100000);
  MyTimer5.attachInterrupt(Timer5_IRQ);
  MyTimer5.start();
}

void Timer5_IRQ(void) {
  mqttClient.poll();
}

void onMqttMessage(int msgSize) {
  messageSize = msgSize;
  String topic = mqttClient.messageTopic();
  if (topic == run_topic) {
    state = RUN;
  } else if (topic == stop_topic) {
    sinter_stop();
    state = IDLE;
  } else if (topic == ping_topic) {
    echo();
  }
}

void loop() {
  switch (state) {
    case HELLO:
      mqttClient.beginMessage(hello_topic);
      mqttClient.print(123);  // To be changed to nonce
	    mqttClient.endMessage();
      state = IDLE;
    case IDLE:
      break;
    case RUN:
      isRunning = true;
      parseProgram(messageSize / 2);
      runProgram(messageSize / 2);
      isRunning = false;
      state = IDLE;
      break;
  }
}

//========= FOR RUN STATE =========//

void parseProgram(unsigned int len) {
  program_svm_len = len;
  for (unsigned int i = 0; i < len; i++) {
    char msb = (char) mqttClient.read();
    char lsb = (char) mqttClient.read();
    char curr[] = {msb, lsb};
    program_svm[i] = (unsigned char) strtol(curr, 0, 16);
  }
}

void runProgram(int messageSize) {
  sinter_value_t result;
  sinter_fault_t fault = sinter_run(program_svm, program_svm_len, &result);

  mqttClient.beginMessage(display_topic);
  mqttClient.print("Program exited with fault ");
  mqttClient.print(fault);
  mqttClient.print(" and result type ");
  mqttClient.print(result.type);
  mqttClient.print(" (");
  mqttClient.print(result.integer_value);
  mqttClient.print(", ");
  mqttClient.print(result.boolean_value);
  mqttClient.print(", ");
  mqttClient.print(result.float_value);
  mqttClient.print(", ");
  mqttClient.print(result.string_value);
  mqttClient.print(")\n");
  mqttClient.endMessage();
}

//======== FOR PING STATE ========//
void echo() {
  mqttClient.beginMessage(status_topic);
  mqttClient.print(isRunning);
  mqttClient.endMessage();
}
```

## Examples

### 2 + 3 Program

```bash
mosquitto_pub -h localhost -m "adac055000000000100000000000000002000000020200000002030000001146" -t arduino/run
```

### Blink Program

```bash
mosquitto_pub -h localhost -m "adac055000000000100000000000000003000000020600000002010000004402020e0a3d2e0000004c0002060000000a4401020e02e80300004406010e0206000000094401020e02e80300004406010e4d3eccffffff0b0b46" -t arduino/run
```

### Counter

```bash
mosquitto_pub -h localhost -m "adac0550000000001000000000000000030200004416000e4409002d000b0e02000000002d010b0e0a3d650000004c004409003000011302e8030000233d3e00000030000102e8030000113300010b0e3001010201000000113301010b0e3001010210270000253d0e00000002000000003301010b3e010000000b3e010000000b0e3001014417010e4419000e4d3e95ffffff0b0b46" -t arduino/run
```

### To Stop the Program

```bash
mosquitto_pub -h localhost -m "" -t arduino/stop
```
